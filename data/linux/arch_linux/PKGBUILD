# Maintainer: Fincer <fincer89 at hotmail dot com>

pkgname=openra-bleed-tibsunra2
pkgver=r22988.cc3b46b58
pkgrel=1
pkgdesc="An open-source implementation of the Red Alert engine using .NET/mono and OpenGL (git version)"
arch=("any")
url="http://openra.net"
license=("GPL3")
depends=("mono" "ttf-dejavu" "openal" "libgl" "freetype2" "sdl2" "lua51" "hicolor-icon-theme" "desktop-file-utils" "xdg-utils")
optdepends=('zenity')
makedepends=("git")
provides=("openra")
conflicts=("openra-git" "openra")
install=openra-bleed-tibsunra2.install
source=("$pkgname-src::git+https://github.com/OpenRA/OpenRA.git#branch=bleed"
"git://github.com/OpenRA/d2.git"
"git://github.com/OpenRA/ra2.git"
hotfix-makefile-enable-ra-and-ts.patch
hotfix-ra2-withexitoverlay-revert.patch
linux-d2-ra2-make.patch
linux-ra2-common.patch
linux-d2-common.patch
hotfix-makefile-readlink.patch)
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          '2e6ff0bb5f4849449ba85f2fd0639b1a7e58c255'
          '3f276aaf52a205e0f7d9110d76bde395ba68d154'
          '16b3babd6182f0ac0223926cc9933e5952548850'
          '7e163ad3e536d2c5a63376602a525c5269f0499e'
          '1bffe99461ce964c7d3cfea86973728e3f597b57'
          '87d687cb0b2e11050eb4b1b2ae31d46a6656c2f7')

pkgver() {
    cd "$srcdir/$pkgname-src"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
    
build() {

# Move Dune 2 Github files to the right place:
    cd "$srcdir/d2"
    mv ./OpenRA.Mods.D2 "$srcdir/$pkgname-src/"
    mkdir -p "$srcdir/$pkgname-src/mods/d2/"
    mv ./* "$srcdir/$pkgname-src/mods/d2/"

# Get Dune 2 GIT version number for files
    d2_version=git-$(git ls-remote https://github.com/OpenRA/d2.git | head -1 | sed "s/HEAD//" | sed 's/^\(.\{7\}\).*/\1/')
    sed -i "s/Version: {DEV_VERSION}/Version: $d2_version/g" $srcdir/$pkgname-src/mods/d2/mod.yaml
    sed -i "s/maps\/d2\/{DEV_VERSION}/maps\/d2\/$d2_version/g" $srcdir/$pkgname-src/mods/d2/mod.yaml

# Move Red Alert 2 Github files to the right place:
    cd "$srcdir/ra2"
    mv ./OpenRA.Mods.RA2 "$srcdir/$pkgname-src/"
    mkdir -p "$srcdir/$pkgname-src/mods/ra2/"
    mv ./* "$srcdir/$pkgname-src/mods/ra2/"

# Patch OpenRA core files for Tiberian Sun & Red Alert 2
    cd "$srcdir/$pkgname-src"
    for i in $srcdir/*.patch; do patch -Np1 < $i; done

    mkdir $srcdir/$pkgname-src/mods/ra2/bits/{vehicles,themes}

# Get Red Alert 2 GIT version number for files
    ra2_version=git-$(git ls-remote https://github.com/OpenRA/ra2.git | head -1 | sed "s/HEAD//" | sed 's/^\(.\{7\}\).*/\1/')
    sed -i "s/Version: {DEV_VERSION}/Version: $ra2_version/g" $srcdir/$pkgname-src/mods/ra2/mod.yaml
    sed -i "s/maps\/ra2\/{DEV_VERSION}/maps\/ra2\/$ra2_version/g" $srcdir/$pkgname-src/mods/ra2/mod.yaml
    
    cd "$srcdir/$pkgname-src"
    make version
    make dependencies
    make core
    make tools
    make all [DEBUG=false]
}

package() {
    cd "$srcdir/$pkgname-src"
    make prefix=/usr DESTDIR="$pkgdir" install-all
    make prefix=/usr DESTDIR="$pkgdir" install-linux-shortcuts
    make prefix=/usr DESTDIR="$pkgdir" install-linux-mime
    make prefix=/usr DESTDIR="$pkgdir" install-linux-appdata

    rm $pkgdir/usr/lib/openra/mods/common/{OpenRA.Mods.Common.dll.mdb,OpenRA.Mods.Cnc.dll.mdb}
    rm $pkgdir/usr/lib/openra/mods/d2k/OpenRA.Mods.D2k.dll.mdb
    rm $pkgdir/usr/lib/openra/mods/ra2/{build.cake,OpenRA.Mods.RA2.dll.mdb,make.cmd,make.ps1,makefile}
}
